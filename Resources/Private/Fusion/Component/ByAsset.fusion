prototype(Garagist.ImageDirectory:ByAsset) < prototype(Neos.Fusion:Component) {
    @propTypes {
        prefixCopyright = ${PropTypes.string}
        defaultCopyright = ${PropTypes.string}
        imageWidth = ${PropTypes.integer}
    }

    rawList = Garagist.ImageDirectory:Helper.RawList {
        sortBy = 'asset'
    }

    node = ${node}

    prefixCopyright = ${q(this.node).property('imageDirectoryPrefixCopyright')}
    defaultCopyright = ${q(this.node).property('imageDirectoryDefaultCopyright')}
    imageWidth = ${q(this.node).property('imageDirectoryImageWidth')}
    ignoreAssets = ${q(this.node).property('imageDirectoryIgnoreAssets')}
    ignoreDocuments = ${q(this.node).property('imageDirectoryIgnoreDocuments')}

    filter = '[!instanceof Neos.Neos:Shortcut]'

    renderer = Neos.Fusion:Map {
        @process.filter = ${Array.filter(value)}
        items = ${props.rawList}
        itemRenderer = Garagist.ImageDirectory:Helper.AssetDataStructure {
            @if {
                notIgnore = ${Array.indexOf(props.ignoreAssets || [], item.asset) == -1}
                hasDocuments = ${Array.length(this.documents)}
            }
            asset = ${item.asset}
            prefixCopyright = ${props.prefixCopyright}
            defaultCopyright = ${props.defaultCopyright}
            imageWidth = ${props.imageWidth}
            documents = Neos.Fusion:Map {
                @process.filter = ${Array.filter(value)}
                items = ${item.documents}
                itemRenderer = Garagist.ImageDirectory:Helper.DocumentDataStructure {
                    @if.notIgnore = ${(props.filter ? q(item.node).is(props.filter) : true) && Array.indexOf(props.ignoreDocuments || [], item.node) == -1 || false}
                    document = ${item}
                }
            }
        }
    }
}
